cmake_minimum_required(VERSION 3.19)

set(satemu_known_comps static shared)
set(satemu_comp_static NO)
set(satemu_comp_shared NO)
foreach (satemu_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (satemu_comp IN_LIST satemu_known_comps)
        set(satemu_comp_${satemu_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "satemu does not recognize component `${satemu_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (satemu_comp_static AND satemu_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "satemu `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(satemu_static_targets "${CMAKE_CURRENT_LIST_DIR}/satemu-static-targets.cmake")
set(satemu_shared_targets "${CMAKE_CURRENT_LIST_DIR}/satemu-shared-targets.cmake")

macro(satemu_load_targets type)
    if (NOT EXISTS "${satemu_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "satemu `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${satemu_${type}_targets}")
endmacro()

if (satemu_comp_static)
    satemu_load_targets(static)
elseif (satemu_comp_shared)
    satemu_load_targets(shared)
elseif (DEFINED satemu_SHARED_LIBS AND satemu_SHARED_LIBS)
    satemu_load_targets(shared)
elseif (DEFINED satemu_SHARED_LIBS AND NOT satemu_SHARED_LIBS)
    satemu_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${satemu_shared_targets}")
        satemu_load_targets(shared)
    else ()
        satemu_load_targets(static)
    endif ()
else ()
    if (EXISTS "${satemu_static_targets}")
        satemu_load_targets(static)
    else ()
        satemu_load_targets(shared)
    endif ()
endif ()
