cmake_minimum_required(VERSION 3.19)

set(Ymir_known_comps static shared)
set(Ymir_comp_static NO)
set(Ymir_comp_shared NO)
foreach (Ymir_comp IN LISTS ${CMAKE_FIND_PACKAGE_NAME}_FIND_COMPONENTS)
    if (Ymir_comp IN_LIST Ymir_known_comps)
        set(Ymir_comp_${Ymir_comp} YES)
    else ()
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "Ymir does not recognize component `${Ymir_comp}`.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
endforeach ()

if (Ymir_comp_static AND Ymir_comp_shared)
    set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
        "Ymir `static` and `shared` components are mutually exclusive.")
    set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
    return()
endif ()

set(Ymir_static_targets "${CMAKE_CURRENT_LIST_DIR}/Ymir-static-targets.cmake")
set(Ymir_shared_targets "${CMAKE_CURRENT_LIST_DIR}/Ymir-shared-targets.cmake")

macro(Ymir_load_targets type)
    if (NOT EXISTS "${Ymir_${type}_targets}")
        set(${CMAKE_FIND_PACKAGE_NAME}_NOT_FOUND_MESSAGE
            "Ymir `${type}` libraries were requested but not found.")
        set(${CMAKE_FIND_PACKAGE_NAME}_FOUND FALSE)
        return()
    endif ()
    include("${Ymir_${type}_targets}")
endmacro()

if (Ymir_comp_static)
    Ymir_load_targets(static)
elseif (Ymir_comp_shared)
    Ymir_load_targets(shared)
elseif (DEFINED Ymir_SHARED_LIBS AND Ymir_SHARED_LIBS)
    Ymir_load_targets(shared)
elseif (DEFINED Ymir_SHARED_LIBS AND NOT Ymir_SHARED_LIBS)
    Ymir_load_targets(static)
elseif (BUILD_SHARED_LIBS)
    if (EXISTS "${Ymir_shared_targets}")
        Ymir_load_targets(shared)
    else ()
        Ymir_load_targets(static)
    endif ()
else ()
    if (EXISTS "${Ymir_static_targets}")
        Ymir_load_targets(static)
    else ()
        Ymir_load_targets(shared)
    endif ()
endif ()
